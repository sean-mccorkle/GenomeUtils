# Program:      codons
# Programmer:   Sean R. McCorkle
# Language:     icon
#
# Description:  First pass at an icon program to histogram codon frequency
#               in a string
#

link  io

procedure main()

   w := 10
   t := [ table( 0 ), table( 0 ), table( 0 ) ];

   seq := read_fasta()

   every i := 1 to *seq - 2 do
        t[ ((i-1) % 3 ) + 1 ][seq[i+:3]] +:= 1

   codon_set := set();
   every insert( codon_set, key( t[ (1 to 3) ] ) )

   write( "\n",
          right( "codon", w ), right( "Frame 1", w ), right( "Frame 2", w ),
          right( "Frame 3", w ), "\n" )
   every c := !sort( codon_set ) do
      {
       write( right( c, w ), right( t[1][c], w ), 
                 right( t[2][c], w ), right( t[3][c], w) )
      }



end


