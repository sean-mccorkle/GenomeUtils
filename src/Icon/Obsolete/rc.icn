# Program:      rc
# Programmer:   Sean R. McCorkle
# Language:     Icon
# Description:  Reads a sequence file (fasta format), prints out the
#               reverse complement (fasta format)
#
# Usage:        rc [-a <str>] [-hv] [-t <str>] [<file> ...]
#
#               where <seq file> contains a single DNA sequence (fasta).
#               a single hyphen (-) can be used as a file name to specify
#               stdin.  If no files are specified, stdin is scanned.
#               If not specified, stdin is used.
#
# Options:
#
#              -a <str> append <str> to each title (default is "")
#              -h       print help, then exit
#              -t <str> use <str> as title on FASTA output (overrides -a)
#              -v       print version, then exit
#
# $Id: rc.icn,v 0.4 1999/11/06 04:27:52 mccorkle Exp mccorkle $
#

link  io
link  options

                             ################
                             # Main Program #
                             ################


procedure main( args )

    local opts, f, s, title

    opts := options( args, "a: -h -t: -v", opt_err )
    if member( opts, "h" )  then { help(); exit() }
    if member( opts, "v" )  then { version(); exit() }

    if ( *args = 0 ) then push( args, "-" )   # use  stdin if no filenames
    every f := open_file( !args ) do
       {
        while s := read_fasta( f ) do
	   {
            title := s.heading
            if member( opts, "a" )
                then  title ||:= opts["a"]
            if member( opts, "t" )
                then  title := opts["t"]
            write_fasta( &output, rc( s.seq ), title )
           }
        close_file( f )
       }

end


#
# print version number & help
#

procedure help()

    version()
    write( "\nUsage:  rc [-hv] [<file> ...]\n" )
    write( "        a single hyphen (-) can be used as a file name to specify")
    write( "        stdin.  If no files are specified, stdin is scanned.\n" )
    write( "Options:" )
    write( "        -a <str> append <str> to each title")
    write( "        -h       print help" )
    write( "        -t <str> use <str> as title on FASTA output ",
                    "(overrides -a)" )
    write( "        -v       print version" )

end

#
# print version number
#
procedure version()

    "$Revision: 0.4 $" ? { tab( upto( ' ' ) )
                           write( "rc", tab( many( &digits ++ ' .' ) ),
                                  " Reverse-complement sequence(s)"
                                )
		         }
end

#
# slightly more informative option error message
#
procedure opt_err( msg )

   write( &errout, "rc: ", msg, "  (type rc -h for help)" )
   exit( 1 )
end





