#!/usr/bin/perl
# Program:     extract_sequences_list
# Programmer:  Sean R. McCorkle
#              Biological, Environmental and Climate Sciences Dept.
#              Brookhaven National Laboratory
# Language:    perl
# 
# Description: Reads a list of identifiers from a file, one per line.
#              Then passes any fasta sequences whose headers start with
#              any of those ids (assuming the id is the first whitespace
#              separated entry on the header).
#              
# Usage:       extract_sequences_list  [-u]  <wanted_list>  [seqs...]
#
# Options:      -u  unique - don't repeatedly pass the same sequence if it
#                   recurs 
#
use strict;

our ( $opt_u );

use Getopt::Std;

getopts( "u" ) || die "Bad option: only -u recognized\n";

my %want = get_list( (shift) );

my $pass = 0;

while ( <> )
   {
    next if ( /^\s*$/ );
    if ( /^>(\S+)+/ )
       { 
        $pass = $want{$1}; 
        undef( $want{$1} ) if ( $want{$1} && $opt_u );  # clear after first time
       }
    print if ( $pass );
   }


sub  get_list
   {
    my $file = shift;
    my %w = ();

    open( LIST, $file ) || die "Can't open $file: $!\n";
    while ( $_ = <LIST> )
       {
        chomp;
        s/^\s*//;
        s/\s*$//;
        $w{$_}++;
       }
    close( LIST );

    return( %w );
   }
